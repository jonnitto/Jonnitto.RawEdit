prototype(Jonnitto.RawEdit:Content) < prototype(Neos.Fusion:Tag) {
    property = null
    header = ${this.property}
    innerTag = 'span'
    outerTag = ${this.innerTag == 'div' ? 'div' : 'p'}
    innerContent = ${this.property ? q(node).property(this.property) : false}
    innerClass = false
    editable = true

    @if.innerContent = ${this.innerContent || this.property ? true : false}

    @context {
        property = ${this.property}
        header = ${this.header}
        outerTag = ${this.outerTag}
        innerTag = ${this.innerTag}
        innerContent = ${this.innerContent}
        innerClass = ${this.innerClass}
        editable = ${this.editable}
    }

    attributes {
        class = Neos.Fusion:RawArray {
            default = 'column neos-raw-content-contentcollection-container'
        }
    }

    content = Neos.Fusion:Array {
        header = Neos.Fusion:Tag {
            tagName = 'header'
            content = ${header}
            @if.hasHeader = ${header}
        }
        content = Neos.Fusion:Tag {
            tagName = ${outerTag}
            content = Neos.Fusion:Tag {
                attributes.class = ${innerClass}
                tagName = ${innerTag}
                content = ${innerContent}
                @process.contentElementEditable = Neos.Neos:ContentElementEditable {
                    @if.editable = ${editable}
                }
            }

            @process.contentElementWrapping = Neos.Neos:ContentElementWrapping {
                @if.editable = ${editable}
            }
        }
    }
}
